#include "macros.h"
module errors
    use constants, only: dp, int64,lenof_sign
    use Parallel_neci, only: iProcIndex,Root
    use util_mod, only: get_free_unit
    implicit none
    real(dp), allocatable :: numerator_data(:)
    real(dp), allocatable :: imnumerator_data(:)
    real(dp), allocatable :: pophf_data(:)
    real(dp), allocatable :: shift_data(:)
    integer :: Errordebug
    logical :: tGivenEquilibrium
    real(dp) :: Shift_Start, ProjE_Start
    integer :: relaxation_time_shift, relaxation_time_proje

    contains

    !Perform automatic FCIMC blocking analysis, reading in whether we have started varying the shift or not, and which iteration we did so, and
    !returning the mean and error for all energy estimates.
    subroutine error_analysis(tSingPartPhase,iShiftVary,mean_ProjE_re,ProjE_Err_re, &
        mean_ProjE_im,ProjE_Err_im,mean_Shift,Shift_Err,tNoProjEValue,tNoShiftValue, &
        equilshift,equilproje)
        implicit none
        logical , intent(in) :: tSingPartPhase
        integer , intent(inout) :: iShiftVary
        integer , intent(in) , optional :: equilshift,equilproje
        integer :: corrlength_denom,corrlength_num,corrlength_shift,corrlength_imnum
        integer :: equil_time_denom,equil_time_num,equil_time_shift,equil_time_imnum
        real(dp), allocatable :: temp(:)
        integer :: final_corrlength_proj,ierr
        real(dp) :: mean_denom, error_denom, eie_denom
        real(dp) :: mean_num, error_num, eie_num
        real(dp) :: eie_shift
        real(dp) :: mean_imnum, error_imnum, eie_imnum
        real(dp) :: covariance_re, corr_coeff_re, correction_re
        real(dp) :: covariance_im, corr_coeff_im, correction_im
        logical :: tFail_num,tFail_denom,tFail_imnum,tFail_shift
        logical :: tPrintIntermediateBlocking
        logical, intent(out) :: tNoProjEValue,tNoShiftValue
        real(dp), intent(out) :: mean_ProjE_re,ProjE_Err_re,mean_ProjE_im,ProjE_Err_im,mean_Shift,Shift_Err
        character(len=*), parameter :: t_r='Error_analysis'
        logical :: tFailRead

        if(iProcIndex.ne.Root) return   !Just do this in serial

        write(6,"(A)")
        write(6,"(A)") "Calculating approximate errorbar for energy estimates..."
        write(6,"(A)")

        if(present(equilshift).or.present(equilproje)) then
            tGivenEquilibrium = .true.
            if(present(equilshift)) then
                Shift_Start = equilshift
            else
                Shift_Start = 0
            endif
            if(present(equilproje)) then
                ProjE_Start = equilproje
            else
                ProjE_Start = 0
            endif
            iShiftVary = min(ProjE_Start,Shift_Start)
        else
            tGivenEquilibrium = .false.
        endif

        tNoProjEValue = .false.
        tNoShiftValue = .false.
        if(tSingPartPhase.and.(.not.tGivenEquilibrium)) then
            write(6,"(A)") "Calculation has not entered constant growth phase. Error analysis therefore not performed."
            write(6,"(A)") "Direct reblocking of instantaneous energy possible, but this would contain finite sampling bias."
            tNoProjEValue = .true.
            tNoShiftValue = .true.
            return
        else
            write(6,"(A,I12)") "Attempting automatic reblocking analysis on data from iteration: ",iShiftVary
        endif

        !Read and store numerator_data, pophf_data, shift_data, and if complex, also imnumerator_data
        call read_fcimcstats(iShiftVary,tFailRead)
        if(tFailRead) then
            tNoProjEValue = .true.
            tNoShiftValue = .true.
            return
        endif

        ! STEP 2) Performs an preliminary blocking analysis, with a fixed blocklength of 2
        ! This is predominantly to yield the correlation length
        ! Files for plotting (for manual reblocking) are outputted in PLOT files
        if(Errordebug.gt.0) then
            tPrintIntermediateBlocking=.true.
        else
            tPrintIntermediateBlocking=.false.
        endif
        call automatic_reblocking_analysis(pophf_data,2,corrlength_denom,   &
            'PLOT_denom',tPrintIntermediateBlocking,1)
        call automatic_reblocking_analysis(numerator_data,2,corrlength_num, &
            'PLOT_num',tPrintIntermediateBlocking,2)
        call automatic_reblocking_analysis(shift_data,2,corrlength_shift,   &
            'PLOT_shift',tPrintIntermediateBlocking,3)
        if(lenof_sign.eq.2) then
            call automatic_reblocking_analysis(imnumerator_data,2,  &
                corrlength_imnum,'PLOT_imnum',tPrintIntermediateBlocking,4)
        endif

        if(.not.tGivenEquilibrium) then

            ! STEP 3) Attempts an automatic removal of equilibration time
            ! From the data that's had serial correlation removed
            ! (See subroutine for how this is achieved)
            allocate(temp(size(pophf_data)),stat=ierr)
            if(ierr.ne.0) call stop_all(t_r,'Alloc error')
            temp = pophf_data
            call reblock_data(temp,corrlength_denom)
            if(errordebug.gt.0) call print_vector(temp,filename='PLOT_blocked_pophf')
            call attempt_detect_equil_time(temp,equil_time_denom,tFail_denom)
            if(tFail_denom) then
                write(6,"(A)")
                write(6,"(A)") "*** ERROR *** Failure to automatically detect equilibration time for " &
                    & //"projected energy denominator"
                write(6,"(A)") "Skipping blocking analysis of projected energy, and energy estimate " &
                    & //"will be simple average over "
                write(6,"(A)") "all iterations (including growth phase), which may contain correlated " &
                    & //"sampling bias. Use with caution." 
                write(6,"(A)") "Manual reblocking or continued running suggested for accurate " &
                    & //"projected energy estimate."
                tNoProjEValue = .true.
            endif
            deallocate(temp)

            allocate(temp(size(numerator_data)),stat=ierr)
            if(ierr.ne.0) call stop_all(t_r,'Alloc error')
            temp = numerator_data
            call reblock_data(temp,corrlength_num)
            if(errordebug.gt.0) call print_vector(temp,filename='PLOT_blocked_num')
            call attempt_detect_equil_time(temp,equil_time_num,tFail_num)
            if(tFail_num) then
                write(6,"(A)")
                write(6,"(A)") "*** ERROR *** Failure to automatically detect equilibration time for " &
                    & //"projected energy numerator"
                write(6,"(A)") "Skipping blocking analysis of projected energy, and energy estimate " &
                    & //"will be simple average over "
                write(6,"(A)") "all iterations (including growth phase), which may contain correlated " &
                    & //"sampling bias. Use with caution." 
                write(6,"(A)") "Manual reblocking or continued running suggested for accurate " &
                    & //"projected energy estimate."
                tNoProjEValue = .true.
            endif
            deallocate(temp)

            allocate(temp(size(shift_data)),stat=ierr)
            if(ierr.ne.0) call stop_all(t_r,'Alloc error')
            temp = shift_data
            call reblock_data(temp,corrlength_shift)
            if(errordebug.gt.0) call print_vector(temp,filename='PLOT_blocked_shift')
            call attempt_detect_equil_time(temp,equil_time_shift,tFail_shift)
            if(tFail_shift) then
                write(6,"(A)")
                write(6,"(A)") "*** ERROR *** Failure to automatically detect equilibration time for shift value."
                write(6,"(A)") "Skipping blocking analysis and calculation of average shift."
                write(6,"(A)") "Continued running suggested if accurate shift estimate required."
                tNoShiftValue = .true.
            endif
            deallocate(temp)

            if(lenof_sign.eq.2) then
                allocate(temp(size(imnumerator_data)),stat=ierr)
                if(ierr.ne.0) call stop_all(t_r,'Alloc error')
                temp = imnumerator_data
                call reblock_data(temp,corrlength_imnum)
                if(errordebug.gt.0) call print_vector(temp,filename='PLOT_blocked_imnum')
                call attempt_detect_equil_time(temp,equil_time_imnum,tFail_imnum)
                if(tFail_imnum) then
                    write(6,"(A)")
                    write(6,"(A)") "*** ERROR *** Failure to automatically detect equilibration time for " &
                        & //"imaginary projected energy numerator"
                    write(6,"(A)") "Skipping blocking analysis of projected energy, and energy estimate " &
                        & //"will be simple average over "
                    write(6,"(A)") "all iterations (including growth phase), which may contain correlated " &
                        & //"sampling bias. Use with caution." 
                    write(6,"(A)") "Continued running suggested for accurate projected energy estimate."
                    tNoProjEValue = .true.
                endif
                deallocate(temp)
            endif

            ! STEP 4) Removes the equilibration time from the start of the data set
            ! Allow shift and projected energy to have seperate equilibration times
            ! If complex, the equilibration time for projected energy is the longest of all three quantities
            if(.not.tNoProjEValue) then
                relaxation_time_proje=max(equil_time_denom*corrlength_denom,equil_time_num*corrlength_num)
                if(lenof_sign.eq.2) then
                    relaxation_time_proje=max(relaxation_time_proje,equil_time_imnum*corrlength_imnum)
                endif
                write(6,"(A,I8,A)") "Relaxation time for projected energy estimated to be ", relaxation_time_proje, &
                    " update cycles."
            endif
            if(.not.tNoShiftValue) then
                relaxation_time_shift = equil_time_shift*corrlength_shift
                write(6,"(A,I8,A)") "Relaxation time for shift estimated to be ", relaxation_time_shift,    &
                    " update cycles."
            endif

        else
            write(6,"(A,I8,A)") "Relaxation time for projected energy estimated to be ", relaxation_time_proje, &
                " update cycles."
            write(6,"(A,I8,A)") "Relaxation time for shift estimated to be ", relaxation_time_shift," update cycles."
        endif   !Endif automatic relaxation time calculation

        if(.not.tNoProjEValue) then
            call resize(pophf_data,relaxation_time_proje)
            call automatic_reblocking_analysis(pophf_data,2,corrlength_denom,'Blocks_denom',.true.,1)
            call resize(numerator_data,relaxation_time_proje)
            call automatic_reblocking_analysis(numerator_data,2,corrlength_num,'Blocks_num',.true.,2)
            if(lenof_sign.eq.2) then
                call resize(imnumerator_data,relaxation_time_proje)
                call automatic_reblocking_analysis(imnumerator_data,2,corrlength_imnum,'Blocks_imnum',.true.,4)
            endif
        endif
        if(.not.tNoShiftValue) then
            call resize(shift_data,relaxation_time_shift)
            call automatic_reblocking_analysis(shift_data,2,corrlength_shift,'Blocks_shift',.true.,3)
        endif

        ! STEP 5) Now gathers together the properly reblocked data and find statistics
        if(.not.tNoProjEValue) then
            final_corrlength_proj = max(corrlength_num,corrlength_denom)
            if(lenof_sign.eq.2) then
                final_corrlength_proj = max(final_corrlength_proj,corrlength_imnum)
            endif
            if(Errordebug.gt.0) write(6,"(A,I10)") "Final projected energy correlation length",final_corrlength_proj
            call reblock_data(pophf_data,final_corrlength_proj)
            call reblock_data(numerator_data,final_corrlength_proj)
            call analyze_data(pophf_data,mean_denom,error_denom,eie_denom)
            call analyze_data(numerator_data,mean_num,error_num,eie_num)
            write(6,"(A,F20.10,A,G20.8,A,G22.10)") "ProjE_denominator:", mean_denom, " +/- ", error_denom, &
                " Relative error: ", abs(error_denom/mean_denom)
            if(lenof_sign.eq.2) then
                call reblock_data(imnumerator_data,final_corrlength_proj)
                call analyze_data(imnumerator_data,mean_imnum,error_imnum,eie_imnum)
                write(6,"(A,F20.10,A,G20.8,A,G22.10)") "ProjE_numerator (Re):", mean_num, " +/- ", error_num, &
                    " Relative error: ", abs(error_num/mean_num)
                write(6,"(A,F20.10,A,G20.8,A,G22.10)") "ProjE_numerator (Im):", mean_imnum, " +/- ", error_imnum, &
                    " Relative error: ", abs(error_imnum/mean_imnum)
            else
                write(6,"(A,F20.10,A,G20.8,A,G22.10)") "ProjE_numerator:  ", mean_num, " +/- ", error_num, &
                    " Relative error: ", abs(error_num/mean_num)
            endif
        endif
        ! write(6,*) "OVERALL", mean2/mean1, "+-", abs(mean2/mean1)*((abs(error1/mean1))**2.0_dp+(abs(error2/mean2))**2.0_dp)**0.5_dp
        ! this is before the covariance, so don't print it now

        if(.not.tNoShiftValue) then
            !Now, also do this for the shift estimate
            call reblock_data(shift_data,corrlength_shift)
            call analyze_data(shift_data,mean_shift,shift_Err,eie_shift)
            if(Errordebug.gt.0) then
                write(6,"(A,F20.10,A,G20.8,A,G22.10)") "Shift:            ", &
                    mean_shift, " +/- ", shift_Err, " Relative error: ", abs(shift_Err/mean_shift)
            endif
        endif

        ! STEP 6) Refine statistics using covariance
        if(.not.tNoProjEValue) then
            covariance_re=calc_covariance(pophf_data,numerator_data)
            corr_coeff_re=covariance_re/(error_denom*error_num*(size(pophf_data)-1)**2.0_dp)
            if(lenof_sign.eq.2) then
                covariance_im=calc_covariance(pophf_data,imnumerator_data)
                corr_coeff_im=covariance_im/(error_denom*error_imnum*(size(pophf_data)-1)**2.0_dp)
                if(Errordebug.gt.0) then
                    write(6,"(A,F20.8)") "Correlation coefficient (Re) :", corr_coeff_re
                    write(6,"(A,F20.8)") "Correlation coefficient (Im) :", corr_coeff_im
                endif
                if (corr_coeff_im.gt.1.0_dp) call stop_all(t_r,"ERROR: Im correlation coefficient > 1")
                if (corr_coeff_im.lt.-1.0_dp) call stop_all(t_r,"ERROR: Im correlation coefficient < -1")
                correction_im=2.0_dp*corr_coeff_im*error_denom*error_imnum/(mean_denom*mean_imnum)
            else
                if(Errordebug.gt.0) write(6,"(A,F20.8)") "Correlation coefficient:", corr_coeff_re
            endif
            if (corr_coeff_re.gt.1.0_dp) call stop_all(t_r,"ERROR: correlation coefficient > 1")
            if (corr_coeff_re.lt.-1.0_dp) call stop_all(t_r,"ERROR: correlation coefficient < -1")
            correction_re=2.0_dp*corr_coeff_re*error_denom*error_num/(mean_denom*mean_num)

            mean_ProjE_re = mean_num/mean_denom
            ProjE_Err_re = abs(mean_num/mean_denom)*((abs(error_denom/mean_denom))**2.0_dp  &
                +(abs(error_num/mean_num))**2.0_dp)**0.5_dp*(1.0_dp-correction_re)
            if(lenof_sign.eq.2) then
                mean_ProjE_im = mean_imnum/mean_denom
                ProjE_Err_im = abs(mean_imnum/mean_denom)*((abs(error_denom/mean_denom))**2.0_dp    &
                    +(abs(error_imnum/mean_num))**2.0_dp)**0.5_dp*(1.0_dp-correction_im)
                if(ErrorDebug.gt.0) then
                    write(6,"(A,F20.8)") "Covariance correction factor (Re):", 1-correction_re
                    write(6,"(A,F20.8)") "Covariance correction factor (Im):", 1-correction_im
                    write(6,"(A,F20.10,A,G20.8)") "Final projected energy (Re): ", mean_ProjE_re, " +/- ", ProjE_Err_re 
                    write(6,"(A,F20.10,A,G20.8)") "Final projected energy (Im): ", mean_ProjE_im, " +/- ", ProjE_Err_im 
                endif
            else
                if(ErrorDebug.gt.0) then
                    write(6,"(A,F20.8)") "Covariance correction factor:", 1-correction_re
                    write(6,"(A,F20.10,A,G20.8)") "Final projected energy: ", mean_ProjE_re, " +/- ", ProjE_Err_re
                endif
            endif
        endif

        deallocate(pophf_data,numerator_data,shift_data)
        if(lenof_sign.eq.2) deallocate(imnumerator_data)

    end subroutine error_analysis

    subroutine automatic_reblocking_analysis(this,blocklength,corrlength,filename,tPrint,iValue)
    ! Performs automatic reblocking (Flyvbjerg and Petersen)
    ! plus some crude selection scheme
    ! General routine, does not require global data
    ! iValue indicates the value which is currently being blocked:
    ! 1 = denominantor
    ! 2 = real numerator
    ! 3 = shift
    ! 4 = imaginary numerator
        integer, intent(in) :: iValue
        real(dp), intent(in) :: this(:)
        character(len=*), intent(in) :: filename
        logical, intent(in) :: tPrint
        integer :: length
        integer,intent(in) :: blocklength ! this is the minimum step size (e.g. 2)
        integer,intent(out) :: corrlength ! this is the correlation length in iterations (e.g. 2**N)
        integer :: i
        real(dp) :: mean, error, eie
        real(dp), allocatable :: that(:) ! stores this array
        integer :: blocking_events
        real(dp), allocatable :: mean_array(:), error_array(:), eie_array(:)
        integer :: which_element,iunit
        real(dp) :: final_error

        length=size(this,1)
        allocate(that(length))
        that=this
        blocking_events=int(log(real(size(that),dp))/log(2.0_dp)-1.0_dp) 
                ! Yuck! This justs finds the
                ! expected number of reblocking analyses automatically
        allocate(mean_array(blocking_events+1))
        allocate(error_array(blocking_events+1))
        allocate(eie_array(blocking_events+1))
        
        if(tPrint) then
            iunit = get_free_unit()
            open(iunit,file=filename)
            write(iunit,"(A)") "# Block_length   Mean       Error        Error_in_Error"
        endif

        call analyze_data(that,mean,error,eie)
        if(tPrint) write(iunit,*) size(that), mean, error, eie
        mean_array(1)=mean
        error_array(1)=error
        eie_array(1)=eie
        do i=1,blocking_events 
            call reblock_data(that,blocklength)
            call analyze_data(that,mean,error,eie)
            mean_array(i+1)=mean
            error_array(i+1)=error
            eie_array(i+1)=eie
            if(tPrint) write(iunit,*) size(that), mean, error, eie
        enddo
        call check_reblocking_for_monotonic_increase(error_array,tPrint,iValue)
        call find_max_error(error_array,final_error,which_element)
        corrlength=blocklength**(which_element-1)
        if(errordebug.gt.0) then
            write(6,*) "Mean", mean_array(1)
            write(6,*) "Final error", final_error, "number of blocks", length/blocklength**(which_element-1)
            write(6,*) "Correlation length", corrlength
        endif
        if(tPrint) close(iunit)

        deallocate(mean_array,error_array,eie_array)

    end subroutine automatic_reblocking_analysis

    subroutine read_fcimcstats(iShiftVary,tFailRead)
        use SystemData, only: tMolpro
        integer, intent(inout) :: iShiftVary
        logical, intent(out) :: tFailRead
        character(len=1) :: readline
        character(len=*), parameter :: t_r="read_fcimcstats"
        logical :: exists,tRefToZero
        integer :: eof,comments,i,ierr
        integer :: iunit,doubs,WalkersDiffProc,change,Ann,Died,Born
        real(dp) :: shift,rate,reproje,improje,reinstproje,iminstproje
        real(dp) :: AccRat,IterTime,FracFromSing,TotImagTime,HFShift,InstShift
        real(dp) :: denom,renum,imnum,normhf,norm_psi,curr_S2,Avshift,dud,tote
        real(dp) :: curr_S2_init,AbsProjE
        integer(int64) :: TotDets,iters,rewalkers,imwalkers,validdata,datapoints
        integer, dimension(lenof_sign) :: insthf
        
        !Open file (FCIMCStats or FCIQMCStats)
        iunit = get_free_unit()
        if(tMolpro) then
            inquire(file='FCIQMCStats',exist=exists)
            if(.not.exists) call stop_all(t_r,'No FCIQMCStats file found for error analysis')
            OPEN(iunit,file='FCIQMCStats',status='old',action='read',position='rewind')
            write(6,"(A)") "Reading back in FCIQMCStats datafile..."
        else
            inquire(file='FCIMCStats',exist=exists)
            if(.not.exists) call stop_all(t_r,'No FCIMCStats file found for error analysis')
            OPEN(iunit,file='FCIMCStats',status='old',action='read',position='rewind')
            write(6,"(A)") "Reading back in FCIMCStats datafile..."
        endif

        comments=0
        datapoints=0
        validdata=0
        tRefToZero = .false.
        do while(.true.)

            read(iunit,"(A)",advance='no',iostat=eof) readline
            if(eof.lt.0) then
                exit
            elseif(eof.gt.0) then
                call stop_all(t_r,"Error reading FCIMCStats file")
            endif

            if(readline.ne.'#') then
                !Valid data on line

                if(lenof_sign.eq.2) then
                    !complex fcimcstats
                    read(iunit,"(I12,G16.7,2I10,2I12,4G17.9,3I10,&
                                          &G13.5,I12,G13.5,G17.5,I13,G13.5,8G17.9)") &
                        iters, &                !1.
                        shift, &                              !2.
                        change, &   !3.
                        rate, &   !4.
                        rewalkers, &                       !5.
                        imwalkers, &                       !6.
                        reproje, &                !7.     real \sum[ nj H0j / n0 ]
                        improje, &                   !8.     Im   \sum[ nj H0j / n0 ]
                        reinstproje, &                 !9.     
                        iminstproje, &                    !10.
                        insthf(1), &                         !11.
                        insthf(lenof_sign), &                         !12.
                        doubs, &                         !13.
                        AccRat, &                               !14.
                        TotDets, &                        !15.
                        IterTime, &                             !16.
                        FracFromSing, &     !17.
                        WalkersDiffProc, &                           !18.
                        TotImagTime, &                               !19.
                        HFShift, &                                   !20.
                        InstShift, &                                 !21.
                        denom     !24     |n0|^2  This is the denominator for both calcs
                else
                    read(iunit,"(I12,G16.7,I10,G16.7,I13,3I15,3G17.9,2I10,&
                                          &G13.5,I12,G13.5,G17.5,I13,G13.5,11G17.9)") &
                        Iters, &
                        shift, &
                        change, &
                        rate, &
                        rewalkers, &
                        Ann, &
                        Died, &
                        Born, &
                        reproje, &
                        Avshift, &
                        reinstproje, &
                        insthf(1), &
                        doubs, &
                        AccRat, &
                        totdets, &
                        IterTime, &
                        FracFromSing, &
                        WalkersDiffProc, &
                        TotImagTime, &
                        dud, &
                        HFShift, &
                        InstShift, &
                        tote, &
                        denom
                endif

!                read(iunit,*,iostat=eof) iters
                if(eof.lt.0) then
                    exit
                elseif(eof.gt.0) then
                    call stop_all(t_r,"Error reading FCIMCStats file")
                endif
                datapoints=datapoints+1
                if(iters.gt.iShiftVary) then
                    if(abs(denom).lt.1.0e-5_dp) then
                        !Denominator gone to zero - wipe the stats
                        tRefToZero = .true.
                        validdata=0
                        iShiftVary = Iters + 1
                    else
                        validdata=validdata+1
                    endif
                endif

            else
                !Just read it again without the advance=no to move onto the next line
                read(iunit,"(A)",iostat=eof) readline
                if(eof.lt.0) then
                    exit
                elseif(eof.gt.0) then
                    call stop_all(t_r,"Error reading FCIMCStats file")
                endif
                comments=comments+1
            endif
        enddo

        if(tRefToZero) then
            write(6,"(A)") "After shift varies, reference population goes to zero"
            write(6,"(A,I14)") "Blocking will only start after non-zero population, at iteration ",iShiftVary-1
        endif

        write(6,"(A,I12)") "Number of comment lines found in file: ",comments
        write(6,"(A,I12)") "Number of data points found in file: ",datapoints
        write(6,"(A,I12)") "Number of useable data points: ",validdata

        if(validdata.le.0) then
            write(6,"(A)") "No valid datapoints found in file. Exiting error analysis."
            tFailRead = .true.
            return
        else
            tFailRead = .false.
        endif

        !Allocate arrays
        allocate(numerator_data(validdata),stat=ierr)
        if(lenof_sign.eq.2) then
            allocate(imnumerator_data(validdata),stat=ierr)
        endif
        allocate(pophf_data(validdata),stat=ierr)
        allocate(shift_data(validdata),stat=ierr)
        if(ierr.ne.0) call stop_all(t_r,"Memory allocation error")
        numerator_data(:) = 0.0_dp
        pophf_data(:) = 0.0_dp
        shift_data(:) = 0.0_dp
        rewind(iunit)

        !Now read all the data in again
        i=0
        do while(.true.)

            read(iunit,"(A)",advance='no',iostat=eof) readline
            if(eof.lt.0) then
                exit
            elseif(eof.gt.0) then
                call stop_all(t_r,"Error reading FCIMCStats file")
            endif

            if(readline.ne.'#') then
                !Valid data on line

                if(lenof_sign.eq.2) then
                    !complex fcimcstats
                    read(iunit,"(I12,G16.7,2I10,2I12,4G17.9,3I10,&
                                          &G13.5,I12,G13.5,G17.5,I13,G13.5,8G17.9)") &
                        iters, &                !1.
                        shift, &                              !2.
                        change, &   !3.
                        rate, &   !4.
                        rewalkers, &                       !5.
                        imwalkers, &                       !6.
                        reproje, &                !7.     real \sum[ nj H0j / n0 ]
                        improje, &                   !8.     Im   \sum[ nj H0j / n0 ]
                        reinstproje, &                 !9.     
                        iminstproje, &                    !10.
                        insthf(1), &                         !11.
                        insthf(lenof_sign), &                         !12.
                        doubs, &                         !13.
                        AccRat, &                               !14.
                        TotDets, &                        !15.
                        IterTime, &                             !16.
                        FracFromSing, &     !17.
                        WalkersDiffProc, &                           !18.
                        TotImagTime, &                               !19.
                        HFShift, &                                   !20.
                        InstShift, &                                 !21.
                        denom, &     !24     |n0|^2  This is the denominator for both calcs
                        renum, &   !22.    Re[\sum njH0j] x Re[n0] + Im[\sum njH0j] x Im[n0]   No div by StepsSft
                        imnum, &       !23.    Im[\sum njH0j] x Re[n0] - Re[\sum njH0j] x Im[n0]   since no physicality
                        normhf, &
                        norm_psi, &
                        curr_S2
                else
                    read(iunit,"(I12,G16.7,I10,G16.7,I13,3I15,3G17.9,2I10,&
                                          &G13.5,I12,G13.5,G17.5,I13,G13.5,11G17.9)") &
                        Iters, &
                        shift, &
                        change, &
                        rate, &
                        rewalkers, &
                        Ann, &
                        Died, &
                        Born, &
                        reproje, &
                        Avshift, &
                        reinstproje, &
                        insthf(1), &
                        doubs, &
                        AccRat, &
                        totdets, &
                        IterTime, &
                        FracFromSing, &
                        WalkersDiffProc, &
                        TotImagTime, &
                        dud, &
                        HFShift, &
                        InstShift, &
                        tote, &
                        denom, &
                        renum, &
                        normhf, &
                        norm_psi, &
                        curr_S2, curr_S2_init, &
                        AbsProjE
                endif

                if(eof.lt.0) then
                    exit
                elseif(eof.gt.0) then
                    call stop_all(t_r,"Error reading FCIMCStats file")
                endif
                if(iters.gt.iShiftVary) then
                    i=i+1
                    if(tGivenEquilibrium) then
                        !Count the update cycles we are to ignore
                        if(iters.lt.Shift_Start) then
                            relaxation_time_shift = relaxation_time_shift + 1
                        endif
                        if(iters.lt.ProjE_Start) then
                            relaxation_time_proje = relaxation_time_proje + 1
                        endif
                    endif
                    numerator_data(i) = renum
                    if(lenof_sign.eq.2) then
                        imnumerator_data(i) = imnum
                    endif
                    pophf_data(i) = denom
                    shift_data(i) = shift
                endif

            else
                !Just read it again without the advance=no to move onto the next line
                read(iunit,"(A)",iostat=eof) readline
                if(eof.lt.0) then
                    exit
                elseif(eof.gt.0) then
                    call stop_all(t_r,"Error reading FCIMCStats file")
                endif
            endif
        enddo

        if(i.ne.validdata) then
            call stop_all(t_r,"Data arrays not filled correctly")
        endif

        if(shift_data(validdata).eq.0.0_dp.or.pophf_data(validdata).eq.0.0_dp.or. &
            numerator_data(validdata).eq.0.0_dp) then
            call stop_all(t_r,"Data arrays not filled correctly")
        endif

        close(iunit)

    end subroutine read_fcimcstats

    subroutine resize(this,remove)
    ! Takes a chunk of size remove off the beginning of array
    ! General routine, does not require global data

        real(dp), allocatable :: this(:)
        real(dp), allocatable :: new(:)
        integer :: remove
        integer :: length
        integer :: i
        character(len=*), parameter :: t_r="resize"

        if(.not. allocated(this)) call stop_all(t_r,"Error, array not allocated on entry into resize")
        length=size(this,1)
        allocate(new(length-remove))
        new=0.0_dp
        do i=1,length
            if (i.gt.remove) then
                new(i-remove)=this(i)
            endif
        enddo
        if (new(length-remove).eq.0.0_dp) call stop_all(t_r,"Resize failed")
        deallocate(this)
        allocate(this(length-remove))
        this=new
        deallocate(new)

    end subroutine resize

    subroutine attempt_detect_equil_time(this,equil_time,tFail)
    ! General routine, does not require global data
    ! Tries to remove the equilibration time from the start of the array

        real(dp), allocatable :: this(:)
        integer :: length
        real(dp), allocatable :: tmp(:)
        integer, allocatable :: i_tmp(:)
        logical :: swapped
        logical, intent(out) :: tFail
        real(dp) :: store
        integer :: i,i_store
        integer :: equil_time
        character(len=*), parameter :: t_r='attempt_detect_equil_time'

        equil_time=0
        if(Errordebug.gt.0) write(6,*) "Attempting to detect equilibration time"

        if(.not. allocated(this)) call stop_all(t_r,"Error, array not allocated on entry into " &
            & //"attempt_remove_equil_time")
        length=size(this,1)
        allocate(tmp(length))
        allocate(i_tmp(length))
        tmp=this

        do i=1,length
            i_tmp(i)=i
        enddo

        ! bubble sort - yay
        do
            swapped=.false.
            do i=2,length
                if (tmp(i).lt.tmp(i-1)) then
                    store=tmp(i-1)
                    tmp(i-1)=tmp(i)
                    tmp(i)=store
                    i_store=i_tmp(i-1)
                    i_tmp(i-1)=i_tmp(i)
                    i_tmp(i)=i_store
                    swapped=.true.
                endif
            enddo
            if (.not.swapped) exit
        enddo

!        do i=1,length
            !write(6,*) tmp(i),i_tmp(i) ! this just prints the sorted data
                                        ! useful to check routine does what is expected
!        enddo

        ! A crude way to remove equilibration time automatically
        ! is to remove the first value (labelled 1st in i_tmp) in the list
        ! if it's ranked first or last in the sorted list
        ! Then to repeat this process.
        ! This can be done without resorting the list because now the 2nd element
        ! must lie at the (n-1)th position or the 2nd position (depending on where
        ! the first element was).

        tFail=.false.

        if (i_tmp(1).eq.1) then
            if(Errordebug.gt.0) write(6,*) "Equilib time detected at lowest value"
            do i=1,length
                if (i_tmp(i).ne.i) then
                    if(Errordebug.gt.0) write(6,*) "Equilib time detected to be", i-1, "values"
                    equil_time=i-1
                    exit
                endif
                if (i.eq.length) then
                    if(Errordebug.gt.0) then
                        write(6,*) "ERROR: the whole data file would be removed by automatic equilibrium detection"
                    endif
                    tFail = .true.
                    return
                endif
            enddo
        endif
        if (i_tmp(length).eq.1) then
            if(Errordebug.gt.0) write(6,*) "Equilib time detected at the highest value"
            do i=1,length
                if (i_tmp(length-i+1).ne.i) then ! Yuck, integer adding, prone to error
                    if(Errordebug.gt.0) write(6,*) "Equilib time detected to be", i-1, "values"
                    equil_time=i-1
                    exit
                endif
                if (i.eq.length) then
                    if(Errordebug.gt.0) then
                        write(6,*) "ERROR: the whole data file would be removed by automatic equilibrium detection"
                    endif
                    tFail = .true.
                    return
                endif
            enddo
        endif

    end subroutine attempt_detect_equil_time

    subroutine analyze_data(this,mean,error,eie)
    ! Finds the mean, error and error in error for Flyvbjerg and Petersen
    ! blocking analysis
    ! General routine, does not require global data

        real(dp), intent(in) :: this(:)
        real(dp), intent(out) :: mean,error,eie
        real(dp) :: s_dev ! standard deviation
        real(dp) :: mean2 ! squared value mean
        integer :: length
        integer :: i
        real(dp) :: s,s2 ! sum x_i and sum x_i^2

        s=0.0_dp
        s2=0.0_dp
        mean=0.0_dp
        mean2=0.0_dp
        error=0.0_dp
        eie=0.0_dp
        length=size(this,1)
        do i=1,length
            s=s+this(i)
            s2=s2+this(i)**2
        enddo
        mean=s/length
        mean2=s2/length
        error=sqrt(mean2-mean**2.0_dp)/sqrt(real(length-1,dp))

        eie=error/sqrt(2.0_dp*(length-1))

    end subroutine analyze_data

    subroutine reblock_data(this,blocklength)
    ! Reduces the data vector for Flyvbjerg and Petersen blocking analysis
    ! by a factor of blocklength (commonly 2, only tested for 2)
    ! General routine, does not require global data
        
        real(dp), allocatable :: this(:)
        integer :: length,new_length,ind_end,i,j
        real(dp), allocatable :: tmp(:)
        integer,intent(in) :: blocklength
        integer :: ierr
        character(len=*), parameter :: t_r='reblock_data'

        if(.not. allocated(this)) call stop_all(t_r,"Error, array not allocated on entry into "&
            & //"reblock_data")
        length=size(this,1)
        new_length=length/blocklength ! truncates towards zero deliberate, loses data
        !write(6,*) "Length is", length
        !write(6,*) "Will be reduced to", new_length
        !write(6,*) "Loss of data", length-new_length*blocklength
        allocate(tmp(new_length),stat=ierr)
        if (ierr < 0) stop
        tmp=0.0_dp
        j=1 ! lazy but foolproof - counting elements
        do i=1,length,blocklength
            ind_end=i+blocklength-1 ! integer addition is disgusting
            if (ind_end.le.length) then 
                tmp(j)=average_vector(this(i:ind_end))
            endif
            j=j+1
        enddo
        if (tmp(new_length).eq.0.0_dp) call stop_all(t_r,"Whole length of new vector not properly used")
        deallocate(this)
        allocate(this(new_length))
        this=0.0_dp
        this=tmp
        deallocate(tmp)
        
    end subroutine reblock_data

    function average_vector(this)
    ! Returns average of a vector
    ! General routine, does not require global data
        
        real(dp) :: this(:)
        integer :: length
        integer :: i
        real(dp) :: s ! sum of array elements
        real(dp) :: average_vector
        
        s=0
        length=size(this,1)
        do i=1,length
            s=s+this(i)
        enddo
        if (length.ne.0) then
            average_vector=s/length
        endif

    end function average_vector

    subroutine print_vector(this,filename)
    ! Just prints a vector (useful for debug)
    ! General routine, does not require global data
        
        real(dp) :: this(:)
        character(len=*), intent(in), optional :: filename
        integer :: length
        integer :: i,iunit
        
        length=size(this,1)
        iunit = get_free_unit()
        if (present(filename)) open(iunit,file=filename)
        do i=1,length
            if (present(filename)) then
                write(iunit,*) i,this(i)
            else
                write(6,*) i,this(i)
            endif
        enddo
        if (present(filename)) close(iunit)

    end subroutine print_vector

    subroutine check_reblocking_for_monotonic_increase(these_errors,tPrint,iValue)
    ! One of the simplest checks on the errors for F&P blocking analysis
    ! is to look for a monotonic increase in errors
    ! which indicates no tail-off/plateauing.
    ! General routine, does not require global data
    ! iValue indicates the value which is currently being blocked:
    ! 1 = denominantor
    ! 2 = real numerator
    ! 3 = shift
    ! 4 = imaginary numerator

        real(dp), intent(in) :: these_errors(:)
        logical , intent(in) :: tPrint
        integer , intent(in) :: iValue
        integer :: length
        integer :: i
        logical :: monotonic
        character(len=*), parameter :: t_r='check_reblocking_for_monotonic_increase'

        monotonic=.true.
        length=size(these_errors)
        do i=2,length 
            if (these_errors(i).lt.these_errors(i-1)) monotonic=.false.
        enddo
        if (monotonic.and.tPrint) then
            if(iValue.eq.1) then
                write(6,"(A)") "WARNING: Error increases monotonically on the blocking graph for " &
                    & //"*denominator of projected energy*" 
            elseif(iValue.eq.2) then
                write(6,"(A)") "WARNING: Error increases monotonically on the blocking graph for " &
                    & //"*numerator of projected energy*" 
            elseif(iValue.eq.3) then
                write(6,"(A)") "WARNING: Error increases monotonically on the blocking graph for *shift*" 
            elseif(iValue.eq.4) then
                write(6,"(A)") "WARNING: Error increases monotonically on the blocking graph for " &
                    & //"*imaginary numerator of projected energy*" 
            else
                call stop_all(t_r,"Unknown iValue passed in")
            endif
            write(6,"(A)") "         whilst performing Flyvbjerg and Petersen blocking analysis."
            write(6,"(A)") "         Inspect BLOCKING files carefully. Manual reblocking may be necessary."
        elseif(monotonic.and.(ErrorDebug.gt.0)) then
            if(iValue.eq.1) then
                write(6,"(A)") "WARNING: Error increases monotonically on the blocking graph for " &
                    & //"*denominator of projected energy*" 
            elseif(iValue.eq.2) then
                write(6,"(A)") "WARNING: Error increases monotonically on the blocking graph for " &
                    & //"*numerator of projected energy*" 
            elseif(iValue.eq.3) then
                write(6,"(A)") "WARNING: Error increases monotonically on the blocking graph for *shift*" 
            elseif(iValue.eq.4) then
                write(6,"(A)") "WARNING: Error increases monotonically on the blocking graph for " &
                    & //"*imaginary numerator of projected energy*" 
            else
                call stop_all(t_r,"Unknown iValue passed in")
            endif
            write(6,"(A)") "         whilst performing Flyvbjerg and Petersen blocking analysis. If this warning"
            write(6,"(A)") "         appears after equilibration time has been removed, then inspect"
            write(6,"(A)") "         BLOCKING files carefully"
        endif

    end subroutine check_reblocking_for_monotonic_increase

    subroutine find_max_error(these_errors,error,which_element)
    ! One of the simplest ways to choose the error in F+P reblocking
    ! is to choose the largest error that's not the one of the last two points
    ! (which guarentees 8 bits of data)
    ! General routine, does not require global data

        real(dp) :: these_errors(:), error
        integer :: length,which_element,i

        length=size(these_errors,1)
        error=these_errors(1)
        which_element=1
        do i=2,length-2
            if (these_errors(i).gt.error) then
                error=these_errors(i)
                which_element=i
            endif
        enddo

    end subroutine find_max_error

    function calc_covariance(this,that)
    ! Covariance between two arrays
    ! General routine, no global data
        
        real(dp), intent(in) :: this(:),that(:)
        integer :: length1,length2
        integer :: i
        real(dp) :: sx,sy,sxy ! sums
        real(dp) :: meanx,meany
        real(dp) :: mean_this, mean_that
        real(dp) :: calc_covariance
        character(len=*), parameter :: t_r='calc_covariance'

        length1=size(this)
        length2=size(that)
        if (length1.ne.length2) call stop_all(t_r,"ERROR: Something has gone very wrong indeed...")
        sx=0.0_dp
        sy=0.0_dp
        do i=1,length1
            sx=sx+this(i)
            sy=sy+that(i)
            if(Errordebug.gt.0) write (6,*) this(i), that(i)
        enddo
        meanx=sx/length1
        meany=sy/length1
        sxy=0.0_dp
        do i=1,length1
            sxy=sxy+(this(i)-meanx)*(that(i)-meany)
        enddo
        calc_covariance=sxy/length1

    end function calc_covariance

end module
